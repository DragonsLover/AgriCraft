buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}
repositories {
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name "squeek"
        url "http://www.ryanliptak.com/maven/"
    }
}

apply plugin: 'forge'
apply plugin: 'java'
apply plugin: 'sonar-runner'
apply plugin: 'jacoco'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties();
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

ext.modVersion = "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
version = "${config.version.minecraft}-${modVersion}"
group = "com.InfinityRaider.AgriCraft" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AgriCraft"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
    runDir = "eclipse"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_nodoc_20150203"

    replaceIn "reference/Reference.java"
    replace "@MAJOR@", config.version.mod.major
    replace "@MINOR@", config.version.mod.minor
    replace "@REVIS@", config.version.mod.revis
}

dependencies {
    // http://files.minecraftforge.net/CodeChickenLib/
    compile "codechicken:CodeChickenLib:1.8-1.1.2.131:dev"

    // http://chickenbones.net/maven
    compile "codechicken:CodeChickenCore:1.8-1.0.5.34:dev"
    compile "codechicken:NotEnoughItems:1.8-1.0.5.87:dev"

    // http://mobiusstrip.eu/maven
    compile "mcp.mobius.waila:Waila:1.6.0_B1_1.8.1:dev"

    // Test dependencies
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.10.8"
    testCompile "org.powermock:powermock-module-junit4:1.6.1"
    testCompile "org.powermock:powermock-api-mockito:1.6.1"
}

allprojects {
    if (System.getenv("TRAVIS_BUILD_NUMBER") != null) {
        version += "-b${System.getenv("TRAVIS_BUILD_NUMBER")}"
    }
}

processResources {
    inputs.property "version", project.modVersion
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            expand ([
                    'version': project.modVersion,
                    'mcversion': project.minecraft.version])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    test {
        java {
            compileClasspath += api.output
            runtimeClasspath += api.output
        }
    }
}
